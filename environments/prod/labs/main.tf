# environments/prod/labs — PERS + MSH spokes (session hosts stay PS)
# CIDRs from legacy platform/pers params/prd/config.yml (VERIFIED).

locals {
  location = var.location
  env      = var.environment
}

module "tags_pers" {
  source = "../../../modules/tags"

  workload    = "vdi-pers"
  environment = local.env
  region      = local.location
  mandatory   = var.mandatory_tags
}

module "tags_mult" {
  source = "../../../modules/tags"

  workload    = "vdi-mult"
  environment = local.env
  region      = local.location
  mandatory   = var.mandatory_tags
}

module "rg_pers_name" {
  source = "../../../modules/naming"

  resource_type   = "resource_group"
  location        = local.location
  subscription_id = var.subscription_code
  environment     = local.env
  description     = "pers-labs"
}

module "rg_mult_name" {
  source = "../../../modules/naming"

  resource_type   = "resource_group"
  location        = local.location
  subscription_id = var.subscription_code
  environment     = local.env
  description     = "mult-labs"
}

resource "azurerm_resource_group" "pers" {
  name     = module.rg_pers_name.name
  location = local.location
  tags     = module.tags_pers.tags
}

resource "azurerm_resource_group" "mult" {
  name     = module.rg_mult_name.name
  location = local.location
  tags     = module.tags_mult.tags
}

# PERS lab spokes — for_each over map from config.yml net_lab_core_pers_*
module "spoke_pers" {
  source   = "../../../modules/core/spoke-pers"
  for_each = var.pers_spokes

  name                = "pers-${each.key}"
  resource_group_name = azurerm_resource_group.pers.name
  location            = local.location
  subscription_id     = var.subscription_code
  environment         = local.env
  unique_id           = each.key

  address_space = each.value.address_space
  dns_servers   = var.dns_servers
  subnets = {
    "AVDSubnet" = {
      address_prefixes = each.value.avd_subnet
    }
  }

  hub01_id = var.hub01_id
  tags     = module.tags_pers.tags
}

# MSH lab spokes — dual hub + UDR scaffold (Hub02 VPN next-hop still PENDING)
module "spoke_msh" {
  source   = "../../../modules/core/spoke-msh"
  for_each = var.msh_spokes

  name                = "mult-${each.key}"
  resource_group_name = azurerm_resource_group.mult.name
  location            = local.location
  subscription_id     = var.subscription_code
  environment         = local.env
  unique_id           = each.key

  address_space = each.value.address_space
  dns_servers   = var.dns_servers
  subnets = {
    for name, cidr in each.value.avd_subnets : name => {
      address_prefixes      = [cidr]
      associate_route_table = true
    }
  }

  hub01_id                 = var.hub01_id
  hub02_id                 = var.hub02_id
  hub01_firewall_private_ip = var.hub01_firewall_private_ip

  tags = module.tags_mult.tags
}

# FSLogix storage (MSH) — profile ops stay PS
module "storage_fslogix" {
  count  = var.enable_fslogix ? 1 : 0
  source = "../../../modules/core/storage-fslogix"

  name                = "mult"
  resource_group_name = azurerm_resource_group.mult.name
  location            = local.location
  subscription_id     = var.subscription_code
  environment         = local.env
  unique_id           = "01"

  azure_files_authentication = {
    directory_type = "AADKERB"
  }

  shares = {
    "profiles" = { quota_gb = 5120 }
  }

  tags = module.tags_mult.tags
}
